#!XX_PERL@
use strict;
use warnings;

# Copyright 1999-2005 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: /var/cvsroot/gentoo-src/ufed/ufed.pl,v 1.45 2005/10/18 11:14:33 truedfx Exp $

use lib qw{XX_perldir@};
use Portage;

my $version = 'XX_PACKAGE_VERSION@';

my %use_descriptions;
my %masked_descriptions;

sub finalise;
sub flags_dialog;
sub read_use_descs;
sub save_flags;

delete $Portage::all_flags{'*'};

read_use_descs;

delete @use_descriptions{qw(bootstrap build)};

$Portage::make_conf_flags{'-*'} = 1
	if defined $Portage::make_conf_flags{'*'}
		&& !$Portage::make_conf_flags{'*'};

for(keys %Portage::all_flags) {
	@{$use_descriptions{$_}} = "(Unknown)"
		if not exists $use_descriptions{$_};
}
@{$use_descriptions{'-*'}} = 'Never enable any flags other than those specified in make.conf';

for(@Portage::archs) {
	delete $Portage::default_flags{$_};
	delete $Portage::all_flags{$_};
	delete $use_descriptions{$_};
}
for my $flag (keys %Portage::use_masked_flags) {
	my $masked = 1;
	for my $mask (values %{$Portage::use_masked_flags{$flag}}) {
		last if not($masked &&= $mask);
	}
	if($masked) {
		if (defined($use_descriptions{$flag})) {
			@{$masked_descriptions{$flag}} = @{$use_descriptions{$flag}};
			for (my $i = 0; $i < scalar @{$masked_descriptions{$flag}}; ++$i) {
				$masked_descriptions{$flag}->[$i] =~ s/^\[[lg]\]/[m]/ ;
				$masked_descriptions{$flag}->[$i] =~ s/^\[L\]/[M]/ ;
			}
		}
		delete $use_descriptions{$flag};
		delete $Portage::default_flags{$flag};
		delete $Portage::all_flags{$flag};
	}
}

flags_dialog;

# Take a list and return it ordered the following way:
# Put "-*" first, followed by enabling flags and put
# disabling flags to the end.
# Parameters: list of flags
sub finalise {
	my @arg = @_;
	my @result = sort {
		($a ne '-*') <=> ($b ne '-*')
		||
		($a =~ /^-/) <=> ($b =~ /^-/)
		||
		$a cmp $b
	} @arg;
	return @result;
}

# Launch the curses inteface. Communication is done using
# pipes. Waiting for pipe read/write to finish is done
# automatically.
# No parameters accepted.
sub flags_dialog {
	use POSIX ();
	POSIX::dup2 1, 3;
	POSIX::dup2 1, 4;
	my ($iread, $iwrite) = POSIX::pipe;
	my ($oread, $owrite) = POSIX::pipe;
	my $child = fork;
	die "fork() failed\n" if not defined $child;
	if($child == 0) {
		POSIX::close $iwrite;
		POSIX::close $oread;
		POSIX::dup2 $iread, 3;
		POSIX::close $iread;
		POSIX::dup2 $owrite, 4;
		POSIX::close $owrite;
		my $interface = 'ufed-curses';
		exec { "XX_libexecdir@/$interface" } $interface or
		do { print STDERR "Couldn't launch $interface\n"; exit 3 }
	}
	POSIX::close $iread;
	POSIX::close $owrite;
	my $outTxt = "";

	# Write masked flags first so they sort at the beginning of the list
	for my $flag (sort { uc $a cmp uc $b } keys %masked_descriptions) {
		$outTxt .= sprintf ("(%s) %s (%s%s) %d\n", $flag,
					defined($Portage::make_conf_flags{$flag})
						? $Portage::make_conf_flags{$flag} ? 'on' : 'off' : 'def',
					exists($Portage::make_defaults_flags{$flag})
						? $Portage::make_defaults_flags{$flag} ? '+' : '-' : ' ',
					exists($Portage::make_conf_flags{$flag})
						? $Portage::make_conf_flags{$flag}     ? '+' : '-' : ' ',
					scalar @{$masked_descriptions{$flag}} );
		$outTxt .= sprintf ("%s\n", $_) for(@{$masked_descriptions{$flag}});
	}

	# Then write regular flags
	for my $flag (sort { uc $a cmp uc $b } keys %use_descriptions) {
		$outTxt .= sprintf ("%s %s (%s%s) %d\n", $flag,
					defined($Portage::make_conf_flags{$flag})
						? $Portage::make_conf_flags{$flag} ? 'on' : 'off' : 'def',
					exists($Portage::make_defaults_flags{$flag})
						? $Portage::make_defaults_flags{$flag} ? '+' : '-' : ' ',
					exists($Portage::make_conf_flags{$flag})
						? $Portage::make_conf_flags{$flag}     ? '+' : '-' : ' ',
					scalar @{$use_descriptions{$flag}}  );
		$outTxt .= sprintf ("%s\n", $_) for(@{$use_descriptions{$flag}});
	}

	# Now let the interface know of the result
	if (open my $fh, '>&=', $iwrite) {
		print $fh $outTxt;
		close $fh;
	} else {
		die "Couldn't let interface know of flags\n";
	}
	POSIX::close $iwrite;
	wait;
	if(POSIX::WIFEXITED($?)) {
		my $rc = POSIX::WEXITSTATUS($?);
		if($rc==0) {
			open my $fh, '<&=', $oread or die "Couldn't read output.\n";
			my @flags = grep { $_ ne '--*' } do { local $/; split /\n/, <$fh> };
			close $fh;
			save_flags finalise @flags;
		} elsif($rc==1)
		{ print "Cancelled, not saving changes.\n" }
		exit $rc;
	} elsif(POSIX::WIFSIGNALED($?))
	{ kill POSIX::WTERMSIG($?), $$ }
	else
	{ exit 127 }
	return;
}

# Build the global %use_descriptions hash.
# Parsed files are:
# 1. PORTDIR/profiles/use.desc
# 2. PORTDIR/profiles/use.local.desc
# No parameters accepted.
# @todo : The local descriptions must be written in a different
#         way (how?) to allow the wanted filtering/distinction
#         between global/local flags.
sub read_use_descs {
	my %_use_descriptions;
	for my $dir(@Portage::portagedirs) {
		for(Portage::noncomments "$dir/profiles/use.desc") {
			my ($flag, $desc) = /^(.*?)\s+-\s+(.*)$/ or next;
			$_use_descriptions{$flag}{$desc} = 1;
		}
	}
	my %_use_local_descriptions;
	for my $dir(@Portage::portagedirs) {
		for(Portage::noncomments "$dir/profiles/use.local.desc") {
			my ($pkg, $flag, $desc) = /^(.*?):(.*?)\s+-\s+(.*)$/ or next;
			$_use_local_descriptions{$flag}{$desc}{$pkg} = 1;
		}
	}
	# Record all global flags first, their description is printed first
	# in the ncurses interface as well.
	for my $key (sort keys %_use_descriptions) {
		for my $desc (sort keys %{$_use_descriptions{$key}}) {
			push @{$use_descriptions{$key}}, "[g] " . $desc;
		}
	}

	# Add local flags
	for my $key (sort keys %_use_local_descriptions) {
		for my $desc (sort keys %{$_use_local_descriptions{$key}}) {
			my $flagPrefix = "l";
			my @pkgs = ();
			for my $pkg (sort keys %{$_use_local_descriptions{$key}{$desc}}) {
				$flagPrefix = "L" if (Portage::have_package($pkg));
				push @pkgs, $pkg;
			}
			local $"=", ";
			push @{$use_descriptions{$key}}, sprintf("[%s] (%s) %s", $flagPrefix, "@pkgs", $desc);
		}
	}
	return;
}

# Write given list of flags back to make.conf if
# the file has not been changed since reading it.
# Parameters: list of flags
sub save_flags {
	my (@flags) = @_;
	my $BLANK = qr{(?:[ \n\t]+|#.*)+};              # whitespace and comments
	my $UBLNK = qr{(?:                              # as above, but scan for #USE=
		[ \n\t]+ |
		\#[ \t]*USE[ \t]*=.*(\n?) | # place capture after USE=... line
		\#.*)+}x;
	my $IDENT = qr{([^ \\\n\t'"{}=#]+)};            # identifiers
	my $ASSIG = qr{=};                              # assignment operator
	my $UQVAL = qr{(?:[^ \\\n\t'"#]+|\\.)+}s;       # unquoted value
	my $SQVAL = qr{'[^']*'};                        # singlequoted value
	my $DQVAL = qr{"(?:[^\\"]|\\.)*"}s;             # doublequoted value
	my $BNUQV = qr{(?:[^ \\\n\t'"#]+|\\\n()|\\.)+}s;# unquoted value (scan for \\\n)
	my $BNDQV = qr{"(?:[^\\"]|\\\n()|\\.)*"}s;      # doublequoted value (scan for \\\n)

	my $contents;

	my $makeconf_name = "$Portage::eprefix/etc/portage/make.conf";
	$makeconf_name = "$Portage::eprefix/etc/make.conf" unless(-r $makeconf_name);
	{
		open my $makeconf, '<', $makeconf_name or die "Couldn't open $makeconf_name\n";
		open my $makeconfold, '>', $makeconf_name . '.old' or die "Couldn't open $makeconf_name.old\n";
		local $/;
		$_ = <$makeconf>;
		print $makeconfold $_;
		close $makeconfold;
		close $makeconf;
	}

	my $sourcing = 0;
	eval {
		# USE comment start/end (start/end of newline character at the end, specifically)
		# default to end of make.conf, to handle make.confs without #USE=
		my($ucs, $uce) = (length, length);
		my $flags = '';
		pos = 0;
		for(;;) {
			if(/\G$UBLNK/gc) {
				($ucs, $uce) = ($-[1], $+[1]) if defined $1;
			}
			last if pos == length;
			my $flagatstartofline = do {
				my $linestart = 1+rindex $_, "\n", pos()-1;
				my $line = substr($_, $linestart, pos()-$linestart);
				$line !~ /[^ \t]/;
			};
			/\G$IDENT/gc or die;
			my $name = $1;
			/\G$BLANK/gc;
			if($name ne 'source') {
			/\G$ASSIG/gc or die;
			/\G$BLANK/gc;
			} else {
				$sourcing = 1;
			}
			die if pos == length;
			if($name ne 'USE') {
				/\G(?:$UQVAL|$SQVAL|$DQVAL)+/gc or die;
			} else {
				my $start = pos;
				/\G(?:$BNUQV|$SQVAL|$BNDQV)+/gc or die;
				my $end = pos;
				# save whether user uses backslash-newline
				my $bsnl = defined $1 || defined $2;
				# start of the line is one past the last newline; also handles first line
				my $linestart = 1+rindex $_, "\n", $start-1;
				# everything on the current line before the USE flags, plus one for the "
				my $line = substr($_, $linestart, $start-$linestart).' ';
				# only indent if USE starts a line
				my $blank = $flagatstartofline ? $line : "";
				$blank =~ s/[^ \t]/ /g;
				# word wrap
				if(@flags != 0) {
					my $length = 0;
					while($line =~ /(.)/g) {
						if($1 ne "\t") {
							$length++;
						} else {
							# no best tab size discussions, please. terminals use ts=8.
							$length&=~8;
							$length+=8;
						}
					}
					my $blanklength = $blank ne '' ? $length : 0;
					# new line, using backslash-newline if the user did that
					my $nl = ($bsnl ? " \\\n" : "\n").$blank;
					my $linelength = $bsnl ? 76 : 78;
					my $flag = $flags[0];
					if($blanklength != 0 || length $flag <= $linelength) {
						$flags   = $flag;
						$length += length $flag;
					} else {
						$flags   = $nl.$flag;
						$length  = length $flag;
					}
					for my $flag (@flags[1..$#flags]) {
						if($length + 1 + length $flag <= $linelength) {
							$flags  .= " $flag";
							$length += 1+length $flag;
						} else {
							$flags  .= $nl.$flag;
							$length  = $blanklength + length $flag;
						}
					}
				}
				# replace the current USE flags with the modified ones
				substr($_, $start, $end-$start) = "\"$flags\"";
				# and have the next search start after our new flags
				pos = $start + 2 + length $flags;
				# and end this
				undef $flags;
				last;
			}
		}
		if(defined $flags) { # if we didn't replace the flags, tack them after the last #USE= or at the end
			$flags = '';
			if(@flags != 0) {
				$flags = $flags[0];
				my $length = 5 + length $flags[0];
				for my $flag(@flags[1..$#flags]) {
					if($length + 1 + length $flag <= 78) {
						$flags  .= " $flag";
						$length += 1+length $flag;
					} else {
						$flags  .= "\n     $flag";
						$length  = 5+length $flag;
					}
				}
			}
			substr($_, $ucs, $uce-$ucs) = "\nUSE=\"$flags\"\n";
		} else { # if we replaced the flags, delete any further overrides
			for(;;) {
				my $start = pos;
				/\G$BLANK/gc;
				last if pos == length;
				/\G$IDENT/gc or die;
				my $name = $1;
				/\G$BLANK/gc;
				if($name ne 'source') {
				/\G$ASSIG/gc or die;
				/\G$BLANK/gc;
				} else {
					$sourcing = 1;
				}
				/\G(?:$UQVAL|$SQVAL|$DQVAL)+/gc or die;
				my $end = pos;
				if($name eq 'USE') {
					substr($_, $start, $end-$start) = '';
					pos = $start;
				}
			}
		}
	};
	die "Parse error when writing make.conf - did you modify it while ufed was running?\n" if $@;

	print STDERR <<EOF if $sourcing;
Warning: source command found in $makeconf_name. Flags may
be saved incorrectly if the sourced file modifies them.
EOF
	{
		open my $makeconf, '>', $makeconf_name or die "Couldn't open $makeconf_name\n";
		print $makeconf $_;
		close $makeconf;
	}
	
	return;
}
